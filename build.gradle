plugins {
    id 'groovy'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.1'
}

group = 'com.cedarsoftware'
description = 'n-cube'
version = '4.6.3-SNAPSHOT'

// Example builds
//gradle clean build 
//gradle clean build publishToMavenLocal
//gradle clean build publish upload closeAndReleaseRepository

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

jar {
    enabled = true
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.2.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.2.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.2.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.2.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-configuration-processor:2.2.1.RELEASE'
    implementation 'org.springframework:spring-orm:5.2.1.RELEASE'
    implementation 'org.springframework:spring-aspects:5.2.1.RELEASE'
    implementation 'org.springframework:spring-context-support:5.2.1.RELEASE'
    implementation 'com.cedarsoftware:json-command-servlet:1.8.3'
    implementation 'com.cedarsoftware:json-io:4.11.1'
    implementation 'com.cedarsoftware:java-util:1.38.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'
    implementation 'org.apache.ivy:ivy:2.4.0'
    implementation 'org.codehaus.groovy:groovy-all:2.4.17'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'org.jetbrains.intellij.deps:trove4j:1.0.20190514'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.hsqldb:hsqldb:2.5.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.1.RELEASE'
    testImplementation 'mysql:mysql-connector-java:8.0.17'
    testImplementation 'org.openjdk.jol:jol-core:0.9'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    archiveClassifier.set('groovydoc')
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives groovydocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            artifact groovydocJar
            from components.java
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.maven
}

tasks.withType(Sign) {
    onlyIf { !version.endsWith("SNAPSHOT") }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.sign(publishing.publications.maven) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'n-cube'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Multi-dimensional Rule Engine'
                url 'https://github.com/jdereg/n-cube'

                scm {
                    connection 'scm:git:git://github.com/jdereg/n-cube.git'
                    developerConnection 'scm:git:git@github.com:jdereg/n-cube.git'
                    url 'https://github.com/jdereg/n-cub'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jdereg'
                        name 'John DeRegnaucourt'
                        email 'jdereg@gmail.com'
                    }
                }
            }
        }
    }
}